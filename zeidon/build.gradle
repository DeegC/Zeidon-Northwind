description ="""
Domain logic for Northwind and the Zeidon objects.

Project name: ${project.name}
"""

configurations {
    repl
    jirb // irb based on jruby
    generateTypescriptFiles
}

buildscript {
    repositories { jcenter() }

    dependencies {
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.3.3'
    }
}

apply plugin: "com.github.jruby-gradle.base"
import com.github.jrubygradle.JRubyExec


// In this section you declare the dependencies for your production and test code
dependencies {
    compile( 'com.quinsoft.zeidon:zeidon-scala:1.5.0-SNAPSHOT') {
        // Exclude some projects to keep the final .war size down.
        exclude module: 'scala-reflect'
        exclude module: 'httpclient'
        exclude module: 'commons-validator'
    }

    compile( 'org.scala-lang:scala-library:2.11.8' ) {
	exclude module: 'scala-xml_2.11'
    }
    
    compile 'org.xerial:sqlite-jdbc:3.19.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'

    repl 'org.scala-lang:scala-compiler:2.11.8'
    repl 'jline:jline:2.13'

    jrubyExec "rubygems:colorize:0.7.7+"

    generateTypescriptFiles 'com.quinsoft.zeidon:javascript:1.5.0-SNAPSHOT'
    generateTypescriptFiles  group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'
}

task generateTypescriptFiles( type: JavaExec ) {
    description 'Generates Typescript files for each LOD.'
    
    def xodDir = file("src/main/resources/northwind/bin")
    def typescriptDir = file("../angular/src/app")

    // Set up properties so this only gets executed if an XOD has changed.
    outputs.upToDateSpec = new org.gradle.api.specs.AndSpec()
    inputs.files "$xodDir/Configuration.XOD"
    outputs.files typescriptDir

    main = "com.quinsoft.zeidon.javascript.GenerateXodsForTypescript"
    classpath( configurations.generateTypescriptFiles, sourceSets.main.runtimeClasspath, '../conf' )
    standardInput System.in
    args( 'Northwind', typescriptDir )
    jvmArgs( '-Xdebug', '-agentlib:jdwp=transport=dt_socket,address=8000,server=y,suspend=n' )
}

jar.dependsOn generateTypescriptFiles

task repl(type:JavaExec) {
  description """
Fire up the Scala REPL, start the Zeidon JOE, and create a task.
Call with: gradle repl --no-daemon --console plain
"""
  
  def initFile = ''

  File.createTempFile("temp",".tmp").with {
    deleteOnExit()
    write '''
       import com.quinsoft.zeidon.standardoe.JavaObjectEngine
       import com.quinsoft.zeidon.scala._

       val oe = JavaObjectEngine.getInstance()
       val task = oe.createTask("Northwind")
    '''
    initFile = absolutePath
  }

  main = "scala.tools.nsc.MainGenericRunner"
  classpath( sourceSets.main.runtimeClasspath, '../conf', configurations.repl )
  standardInput System.in
  args( '-i', initFile, '-usejavacp' )
  jvmArgs( '-DSQLITE_ROOT=../sqlite', '-Djline.inputrc=~/.jline.inputrc', '-Xdebug', '-agentlib:jdwp=transport=dt_socket,address=8000,server=y,suspend=n' )
}

task jirb(type:JRubyExec) {
  description "Run Zeidon under jRuby"


  def initFile = ''

  File.createTempFile("temp",".tmp").with {
    deleteOnExit()
    write '''
require "irb"

    IRB.start(__FILE__)
    '''
    initFile = absolutePath
  }

  script "${initFile}"
}
